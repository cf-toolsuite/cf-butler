plugins {
    id 'com.gorylenko.gradle-git-properties' version '2.0.0'
    id 'org.springframework.boot' version '2.1.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'com.github.ben-manes.versions' version '0.21.0'
	id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '2.0.0'
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}

spotbugs {
    toolVersion = '4.0.0-beta1'
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'low'
    showProgress = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        html {
            enabled = true
        }
        xml {
            enabled = false
        }
    }
}

group = 'io.pivotal'
version = '0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
	mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

ext {
	set('springCloudServicesVersion', '2.1.3.RELEASE')
	set('springCloudVersion', 'Greenwich.SR1')
	set('commons-lang3.version', '3.9')
	set('jackson.version', '2.9.9')
	set('junit-jupiter.version', '5.4.2')
    set('lombok.version', '1.18.8')
	set('mockito.version', '2.27.0')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${springCloudServicesVersion}"
	}
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

configurations {
	// exclude Logback
    implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

jacoco {
	toolVersion = "0.8.3"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
		csv.enabled = false
	}
}

dependencies {
    annotationProcessor('org.projectlombok:lombok')
    implementation('org.projectlombok:lombok')
    implementation('org.springframework.boot:spring-boot-configuration-processor')
    implementation('org.apache.commons:commons-lang3')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    implementation('com.fasterxml.uuid:java-uuid-generator:3.2.0')
    implementation('org.springframework.credhub:spring-credhub-cloud-connector:2.0.1.RELEASE')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-log4j2')
    implementation('org.springframework.boot.experimental:spring-boot-starter-data-r2dbc:0.1.0.BUILD-20190507.184709-3')
    implementation('org.springframework.boot.experimental:spring-boot-autoconfigure-r2dbc:0.1.0.BUILD-20190507.184651-3')
    // We want a prior version of spring-data-r2dbc b/c https://github.com/spring-projects/spring-data-r2dbc/commit/79e32941b51fb74d5356a050a0a66717e3e5da1c requires a dependency on Spring Framework 5.2
    implementation('org.springframework.data:spring-data-r2dbc:1.0.0.BUILD-20190508.084637-64')
    // As of May 30, 2019 we're forced to pin the version of spring-data-relational too
    implementation('org.springframework.data:spring-data-relational:1.1.0.BUILD-SNAPSHOT')
    implementation('io.r2dbc:r2dbc-client:0.8.0.M8')
    implementation('io.r2dbc:r2dbc-pool:0.8.0.M8')
    // FIXME Seems like we shouldn't have a dependency on Spring JDBC
    implementation('org.springframework:spring-jdbc')
    implementation('io.r2dbc:r2dbc-h2:0.8.0.M8')
    implementation('io.r2dbc:r2dbc-postgresql:0.8.0.M8')
    implementation('org.eclipse.jgit:org.eclipse.jgit:5.3.2.201906051522-r')
    implementation('org.cloudfoundry:cloudfoundry-client-reactor:3.15.0.RELEASE')
    implementation('org.cloudfoundry:cloudfoundry-operations:3.15.0.RELEASE')
    implementation('io.projectreactor:reactor-core:3.2.9.RELEASE')
    implementation('io.projectreactor.addons:reactor-extra:3.2.3.RELEASE')
    implementation('io.projectreactor.netty:reactor-netty:0.8.8.RELEASE')
    implementation('io.micrometer:micrometer-registry-prometheus')
    testImplementation('io.projectreactor:reactor-test')
    testImplementation('org.junit.jupiter:junit-jupiter-api')
    testImplementation('org.junit.jupiter:junit-jupiter-params')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
    testImplementation('org.mockito:mockito-core')
    testImplementation('org.mockito:mockito-junit-jupiter')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
	    exclude group: 'junit', module: 'junit'
    }
}

check.dependsOn jacocoTestReport
